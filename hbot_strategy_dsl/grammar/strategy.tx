// klpanagi

StrategyModel:
    (
      ('name:' name=ID)
      ('version:' version=STRING)
      ('author:' author=STRING)?
      ('author_email:' authorEmail=STRING)?
      ('description:' description=STRING)?
      ('type:' base=StrategyType)?
      ('labels:' '[' labels*=STRING[','] ']')?

      (
        'markets'
        markets+=Market
        'end'
      )
      (
        'parameters'
        parameters*=Parameter
        'end'
      )?
    )# // In any order
;

Market:
	'Market'
        'connector:' connector=Connector
        'pairs:' '['pairs*=PairDefinition[','] ']'
    'end'
;


Connector: Exchange;


Exchange: BinanceExchange | AscentExExchange | GateIOExchange;

BinanceExchange:
     'Binance('  apiKey=STRING ',' apiSecret=STRING ')'
;

AscentExExchange:
     'AscentEX('  apiKey=STRING ',' apiSecret=STRING ')'
;

GateIOExchange:
     'GateIO('  apiKey=STRING ',' apiSecret=STRING ')'
;

PairDefinition:
    STRING
;

Parameter:
    'Param'
    (
        ('name:' name=ID)
        ('type:' type=ParameterType)
        ('description:' description=STRING)?
        ('prompt_msg:' promptMessage=STRING)?
        ('default:' defaultValue=ParameterValue)?
        ('keyword:' keyword=STRING)?
        ('dynamic_reconfigure:' dynamicReconfigure=BOOL)?
        ('prompt_on_new:' promptOnNew=BOOL)?
    )# // In any order
    'end'
;

ParameterType:
    'int' | 'float' | 'str' | 'bool' | 'list' | 'dict'
;

ParameterValue:
    STRING | NUMBER | BOOL | List | Dictionary | "null" | 'decimal'
;

List:
    "[" values*=ParameterValue[','] "]"
;

Dictionary:
    "{" members*=DictionaryMember[','] "}"
;

DictionaryMember:
    key=STRING ':' value=ParameterValue
;

ParameterBehaviorType: "static" | "dynamic";

StrategyType: 'Base' | 'Script' | 'PMM' | 'LM';

// Commenting
Comment:
  /\/\/.*$/
;

// === Fully Qualified Names Rule ===
FQN: ID('.'ID)*;
