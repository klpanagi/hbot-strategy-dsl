// klpanagi

StrategyModel:
    (
      ('name:' name=ID)
      ('version:' version=STRING)
      ('author:' author=STRING)?
      ('author_email:' authorEmail=STRING)?
      ('description:' description=STRING)?

      ('markets:' '[' markets+=Market[','] ']')
      ('parameters:' '[' parameters+=Parameter[','] ']')
    )# // In any order

;

Market:
	'Market(' exchange=Exchange ',' pair=MarketPair ')'
;

MarketPair:
	'Pair(' pair=STRING ')'
;

Exchange: BinanceExchange | AscentExExchange;

BinanceExchange:
     'Binance('  apiKey=STRING ',' apiSecret=STRING ')'
;

AscentExExchange:
     'AscentEX('  apiKey=STRING ',' apiSecret=STRING ')'
;

Parameter:
    'Param('
    (
        ('name=' name=ID) ','
        ('type=' type=ParameterType) ','
        ('description=' description=STRING ',')?
        ('prompt_msg=' promptMessage=STRING ',')?
        ('default=' defaultValue=ParameterValue ',')?
        ('keyword=' keyword=STRING ',')?
        ('dynamic_reconfigure=' dynamicReconfigure=BOOL ',')?
        ('prompt_on_new=' promptOnNew=BOOL ',')?
    )# // In any order
    ')'
;

ParameterType:
    'int' | 'float' | 'str' | 'bool' | 'list' | 'dict'
;

ParameterValue:
    STRING | NUMBER | BOOL | List | Dictionary | "null" | 'decimal'
;

List:
    "[" values*=ParameterValue[','] "]"
;

Dictionary:
    "{" members*=DictionaryMember[','] "}"
;

DictionaryMember:
    key=STRING ':' value=ParameterValue
;

ParameterBehaviorType: "static" | "dynamic";

// Commenting
Comment:
  /\/\/.*$/
;

// === Fully Qualified Names Rule ===
FQN: ID('.'ID)*;
